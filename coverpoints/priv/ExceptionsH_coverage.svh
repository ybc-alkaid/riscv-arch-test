///////////////////////////////////////////
//
// RISC-V Architectural Functional Coverage Covergroups
//
// Written: Sadhvi Narayanan sanarayanan@hmc.edu 5 September 2025
//
// Copyright (C) 2024 Harvey Mudd College, 10x Engineers, UET Lahore, Habib University
//
// SPDX-License-Identifier: Apache-2.0
//
////////////////////////////////////////////////////////////////////////////////////////////////

`define COVER_EXCEPTIONSH
covergroup ExceptionsH_cg with function sample(ins_t ins);
    option.per_instance = 0;

    // Include standard RISCV coverpoints
    `include "general/RISCV_coverage_standard_coverpoints.svh"

    // ============================================================================
    // INSTRUCTION COVERPOINTS
    // ============================================================================

    branch: coverpoint ins.current.insn {
        wildcard bins branch = {32'b???????_?????_?????_???_?????_1100011};
    }


    jal: coverpoint ins.current.insn {
        wildcard bins jal = {JAL};
    }


    jalr: coverpoint ins.current.insn {
        wildcard bins jalr = {JALR};
    }


    loadop: coverpoint ins.current.insn {
        wildcard bins lw  = {LW};
    }


    storeop: coverpoint ins.current.insn {
        wildcard bins sw = {SW};
    }


    ecall: coverpoint ins.current.insn {
        bins ecall = {ECALL};
    }


    ebreak: coverpoint ins.current.insn {
        bins ebreak = {EBREAK};
    }


    illegalop: coverpoint ins.current.insn {
        bins zeros = {0};
    }


    hlv_hsv_instr: coverpoint ins.current.insn {
        wildcard bins hlv_w = {HLV_W};
        wildcard bins hsv_w = {HSV_W};
    }


    csrr: coverpoint ins.current.insn {
        wildcard bins csrr = {CSRR};
    }


    instret: coverpoint ins.current.insn[31:20] {
        bins instret_read = {CSR_INSTRET};
    }


    instreth: coverpoint ins.current.insn[31:20] {
        bins instret_read = {CSR_INSTRETH};
    }


    // ============================================================================
    // FAULT CONDITION COVERPOINTS
    // ============================================================================


    adr_LSBs: coverpoint {ins.current.rs1_val + ins.current.imm}[2:0] {
        // Auto fills 000 through 111 for misalignment
    }


    addr_alignment: coverpoint {ins.current.rs1_val + ins.current.imm}[1:0] {
        bins aligned_00 = {2'b00};
        bins misaligned_01 = {2'b01};
    }


    addr_misaligned: coverpoint ({ins.current.rs1_val + ins.current.imm}[1:0] != 2'b00) {
        bins misaligned = {1};
    }


    pc_bit_1: coverpoint ins.current.pc_rdata[1] {
        bins zero = {0};
    }


    imm_bit_1: coverpoint ins.current.imm[1] {
        bins one = {1};
    }


    // Address LSBs for HLV/HSV instructions (rs1 only, no immediate)
    hlv_adr_LSBs: coverpoint ins.current.rs1_val[2:0] {
        // Auto fills 000 through 111 for misalignment
    }

    // ============================================================================
    // DELEGATION REGISTER COVERPOINTS
    // ============================================================================

    // Machine Exception Delegation Register (medeleg)
    medeleg_delegation: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "medeleg", "deleg") {
        bins zeros = {32'h00000000};
        wildcard bins ones = {32'b????_????_1111_??00_1011_0111_1111_111?};
    }


    // Hypervisor Exception Delegation Register (hedeleg)
    hedeleg_delegation: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hedeleg", "deleg") {
        bins zeros = {32'h00000000};
        wildcard bins ones = {32'b????_????_0000_1100_1011_0001_1111_111?};
        `ifdef ZICCLSM_SUPPORTED
                bins walk_1_bit0 = {32'h00000001}; // Instruction address misaligned
        `endif
        bins walk_1_bit1 = {32'h00000002}; // Instruction access fault
        bins walk_1_bit2 = {32'h00000004}; // Illegal instruction
        bins walk_1_bit3 = {32'h00000008}; // Breakpoint
        bins walk_1_bit4 = {32'h00000010}; // Load address misaligned
        bins walk_1_bit5 = {32'h00000020}; // Load access fault
        bins walk_1_bit6 = {32'h00000040}; // Store address misaligned
        bins walk_1_bit7 = {32'h00000080}; // Store access fault
        bins walk_1_bit8 = {32'h00000100}; // Environment call from U-mode
    }


    // U-mode ECALL delegation enabled: M-mode → HS-mode (medeleg bit 8)
    medeleg_ecall_u_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "medeleg", "deleg")[8]) {
        bins delegated = {1};
    }


    // U-mode and VS-mode ECALL delegation enabled: M-mode → HS-mode (medeleg bits 8, 10)
    medeleg_ecall_u_vs_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "medeleg", "deleg")[10:8]) {
        wildcard bins delegated = {3'b1?1};
    }


    // All ECALL bits disabled (no delegation to M-mode)
    medeleg_ecall_disabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "medeleg", "deleg")[11:8]) {
        bins all_ecall_bits_zero = {4'b0000};
    }


    // EBREAK delegation disabled in medeleg (bit 3)
    medeleg_ebreak_disabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "medeleg", "deleg")[3]) {
        bins ebreak_not_delegated = {32'h0};
    }

    // All medeleg bits 0 or 1 (except M/HS-mode ECALL bits 11, 9)
    medeleg_except_ecall: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "medeleg", "deleg") {
        bins all_zero = {32'h00000000};
        wildcard bins all_one_except_m_hs_ecall = {32'b????_????_1111_??00_1011_0101_1111_111?};
    }


    // VU-mode ECALL delegation enabled: HS-mode → VS-mode (hedeleg bit 8)
    hedeleg_ecall_u_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hedeleg", "deleg")[8]) {
        bins delegated = {1};
    }


    // VU-mode and VS-mode ECALL delegation enabled in hedeleg (bits 8, 10)
    hedeleg_ecall_u_vs_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hedeleg", "deleg")[10:8]) {
        wildcard bins not_delegated = {3'b1?1};
    }


    // VU-mode and VS-mode ECALL delegation disabled in hedeleg (bits 8, 10)
    hedeleg_ecall_u_vs_disabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hedeleg", "deleg")[10:8]) {
        wildcard bins not_delegated = {3'b0?0};
    }


    hedeleg_disabled: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hedeleg", "deleg") {
        bins disabled = {32'h00000000};
    }

    // ============================================================================
    // HSTATUS CSR COVERPOINTS (HS-mode traps)
    // ============================================================================

    // Sample Supervisor previous virtual privilege to check which privilege mode we came from
    hstatus_spvp: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_AFTER, "hstatus", "spvp") {
        bins spvp_0 = {0};
        bins spvp_1 = {1};
    }


    // Sample before to check if hypervisor memory access from U-mode was enabled
    hstatus_hu: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hstatus", "hu") {
        bins hu_disabled = {0};
        bins hu_enabled = {1};
    }


    // ============================================================================
    // TRAP VECTOR COVERPOINTS
    // ============================================================================


    vstvec_different_from_stvec: coverpoint
        {get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "vstvec", "mode") !=
        get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "stvec", "mode")} {
        bins different_handlers = {1};
    }


    // ============================================================================
    // HYPERVISOR INSTRUCTION COVERPOINTS
    // ============================================================================

    hlvw_hlvxwu_hsvw_hfencevvma_hfencegvma_instr: coverpoint ins.current.insn {
        wildcard bins hlv_w = {HLV_W};
        wildcard bins hlvx_wu = {HLVX_WU};
        wildcard bins hsv_w = {HSV_W};
        wildcard bins hfence_vvma = {HFENCE_VVMA};
        wildcard bins hfence_gvma = {HFENCE_GVMA};
    }


    hlv_instructions: coverpoint ins.current.insn {
        wildcard bins hlv_b  = {HLV_B};
        wildcard bins hlv_bu = {HLV_BU};
        wildcard bins hlv_h  = {HLV_H};
        wildcard bins hlv_hu = {HLV_HU};
        wildcard bins hlv_w  = {HLV_W};
        wildcard bins hlvx_hu = {HLVX_HU};
        wildcard bins hlvx_wu = {HLVX_WU};
        `ifdef UDB_MXLEN_64
            wildcard bins hlv_d = {HLV_D};
            wildcard bins hlv_wu = {HLV_WU};
        `endif
    }


    hsv_instructions: coverpoint ins.current.insn {
        wildcard bins hsv_b  = {HSV_B};
        wildcard bins hsv_h  = {HSV_H};
        wildcard bins hsv_w  = {HSV_W};
        `ifdef UDB_MXLEN_64
            wildcard bins hsv_d = {HSV_D};
        `endif
    }


    hlvw_hlvxw_hsvw_instr: coverpoint ins.current.insn {
        wildcard bins hlv_w = {HLV_W};
        wildcard bins hlvx_wu = {HLVX_WU};
        wildcard bins hsv_w = {HSV_W};
    }


    sfencevma_hfencevvma_hfencegvma_instr: coverpoint ins.current.insn {
        wildcard bins sfence_vma = {SFENCE_VMA};
        wildcard bins hfence_vvma = {HFENCE_VVMA};
        wildcard bins hfence_gvma = {HFENCE_GVMA};
    }


    wfi: coverpoint ins.current.insn {
        bins wfi = {WFI};
    }


    sret: coverpoint ins.current.insn {
        bins sret = {SRET};
    }


    sfence_sinval_vma: coverpoint ins.current.insn {
        wildcard bins sfence_vma = {SFENCE_VMA};
        wildcard bins sinval_vma = {SINVAL_VMA};
    }


    sfence_vma: coverpoint ins.current.insn {
        wildcard bins sfence_vma = {SFENCE_VMA};
    }


    // ============================================================================
    // CSR ACCESS COVERPOINTS
    // ============================================================================

    vstval_htval: coverpoint ins.current.insn[31:20] {
        bins vstval = {CSR_VSTVAL};
        bins htval = {CSR_HTVAL};
    }


    satp: coverpoint ins.current.insn[31:20] {
        bins satp = {CSR_SATP};
    }


    vsatp: coverpoint ins.current.insn[31:20] {
        bins vsatp = {CSR_VSATP};
    }


    stval: coverpoint ins.current.insn[31:20] {
        bins stval = {CSR_STVAL};
    }

    vstval: coverpoint ins.current.insn[31:20] {
        bins vstval = {CSR_VSTVAL};
    }

    `ifdef UDB_MXLEN_32
        hedelegh: coverpoint ins.current.insn[31:20] {
                bins hedelegh_read = {CSR_HEDELEGH};
        }
    `endif


    // ============================================================================
    // HSTATUS BIT COVERPOINTS
    // ============================================================================

    hstatus_vtw_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hstatus", "vtw")) {
        bins one = {1};
    }


    hstatus_vtsr_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hstatus", "vtsr")) {
        bins one = {1};
    }


    hstatus_vtvm: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hstatus", "vtvm") {
        bins vtvm_disabled = {0};
        bins vtvm_enabled = {1};
    }


    hstatus_vtvm_enabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hstatus", "vtvm")) {
        bins one = {1};
    }


    // ============================================================================
    // MSTATUS BIT COVERPOINTS
    // ============================================================================

    mstatus_tvm_disabled: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "mstatus", "tvm") {
        bins tvm_disabled = {0};
    }


    mstatus_tvm: coverpoint get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "mstatus", "tvm") {
        bins tvm_disabled = {0};
        bins tvm_enabled = {1};
    }


    mstatus_tw_disabled: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "mstatus", "tw") == 0) {
        bins zero = {0};
    }


    // ============================================================================
    // COUNTER ENABLE COVERPOINTS
    // ============================================================================

    scounteren_enabled_ir: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "scounteren", "counteren")[2]) {
        bins enabled = {1};
    }


    scounteren_disabled_ir: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "scounteren", "counteren")[2]) {
        bins disabled = {0};
    }


    hcounteren_enabled_ir: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hcounteren", "counteren")[2]) {
        bins enabled = {1};
    }

    hcounteren_disabled_ir: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "hcounteren", "counteren")[2]) {
        bins disabled = {0};
    }


    mcounteren_enabled_ir: coverpoint (get_csr_val(ins.hart, ins.issue, `SAMPLE_BEFORE, "mcounteren", "counteren")[2]) {
        bins enabled = {1};
    }

    // ============================================================================
    // CROSS COVERAGE
    // ============================================================================

    // cp_hedeleg crosses
    cp_hedeleg_instr_misaligned_branch: cross branch, pc_bit_1, imm_bit_1, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
    cp_hedeleg_instr_misaligned_jal: cross jal, pc_bit_1, imm_bit_1, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
    cp_hedeleg_instr_misaligned_jalr: cross jalr, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
    cp_hedeleg_load_misaligned: cross loadop, adr_LSBs, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
    cp_hedeleg_store_misaligned: cross storeop, adr_LSBs, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
    cp_hedeleg_illegal_instruction: cross illegalop, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
    cp_hedeleg_breakpoint: cross ebreak, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;


    // ECALL delegation crosses
    cp_ecall_to_vs: cross
        ecall,
        priv_mode_vu,
        medeleg_ecall_u_enabled,
        hedeleg_ecall_u_enabled;


    cp_ecall_to_hs: cross
        ecall,
        priv_mode_vs_u_vu,
        medeleg_ecall_u_vs_enabled,
        hedeleg_ecall_u_vs_disabled,
        hstatus_spvp;


    cp_ecall_to_m: cross
        ecall,
        priv_mode_m_hs_vs_u_vu,
        medeleg_ecall_disabled;


    cp_ebreak_to_m: cross
        ebreak,
        priv_mode_m_hs_vs_u_vu,
        medeleg_ebreak_disabled;


    // Trap vector crosses
    cp_vstvec: cross
        ecall,
        priv_mode_vs_vu,
        medeleg_ecall_u_vs_enabled,
        hedeleg_ecall_u_vs_enabled,
        vstvec_different_from_stvec;



    // Virtual instruction exception crosses - VS mode
    cp_virtual_instr_vs_instret: cross hcounteren_disabled_ir, mcounteren_enabled_ir, csrr, instret, priv_mode_vs;
    cp_virtual_instr_vs_execute_hypervisor: cross hlvw_hlvxwu_hsvw_hfencevvma_hfencegvma_instr, priv_mode_vs;
    cp_virtual_instr_vs_read_vstval_htval: cross csrr, vstval_htval, priv_mode_vs;
    cp_virtual_instr_vs_mstatus_satp: cross csrr, satp, mstatus_tvm_disabled, priv_mode_vs;
    cp_virtual_instr_vs_mstatus_vsatp: cross csrr, vsatp, mstatus_tvm_disabled, priv_mode_vs;
    cp_virtual_instr_vs_wfi: cross wfi, hstatus_vtw_enabled, mstatus_tw_disabled, priv_mode_vs;
    cp_virtual_instr_vs_sret: cross sret, hstatus_vtsr_enabled, priv_mode_vs;
    cp_virtual_instr_vs_s_vma_instr: cross sfence_sinval_vma, hstatus_vtvm_enabled, priv_mode_vs;
    cp_virtual_instr_vs_satp: cross csrr, satp, hstatus_vtvm_enabled, priv_mode_vs;
    `ifdef UDB_MXLEN_32
        cp_virtual_instr_vs_rv32_instreth_mcounter: cross csrr, instreth, mcounteren_enabled_ir, hcounteren_disabled_ir, priv_mode_vs;
        cp_virtual_instr_vs_rv32_hedelegh: cross csrr, hedelegh, priv_mode_vs;
    `endif


    // Virtual instruction exception crosses - VU mode
    cp_virtual_instr_vu_instret_1: cross csrr, instret, hcounteren_disabled_ir, mcounteren_enabled_ir, scounteren_enabled_ir, priv_mode_vu;
    cp_virtual_instr_vu_instret_2: cross csrr, instret, hcounteren_enabled_ir, scounteren_disabled_ir, mcounteren_enabled_ir, priv_mode_vu;
    cp_virtual_instr_vu_execute_h: cross hlvw_hlvxwu_hsvw_hfencevvma_hfencegvma_instr, priv_mode_vu;
    cp_virtual_instr_vu_read_vstval_htval: cross csrr, vstval_htval, priv_mode_vu;
    cp_virtual_instr_vu_read_stval: cross csrr, stval, priv_mode_vu;
    cp_virtual_instr_vu_satp: cross csrr, satp, mstatus_tvm_disabled, priv_mode_vu;
    cp_virtual_instr_vu_vsatp: cross csrr, vsatp, mstatus_tvm_disabled, priv_mode_vu;
    cp_virtual_instr_vu_wfi: cross wfi, mstatus_tw_disabled, priv_mode_vu;
    cp_virtual_instr_vu_sret: cross sret, priv_mode_vu;
    cp_virtual_instr_vu_sfence_vma: cross sfence_vma, priv_mode_vu;
    `ifdef UDB_MXLEN_32
        cp_virtual_instr_vu_rv32_instreth_1: cross csrr, instreth, hcounteren_disabled_ir, scounteren_enabled_ir, mcounteren_enabled_ir, priv_mode_vu;
        cp_virtual_instr_vu_rv32_instreth_2: cross csrr, instreth, hcounteren_enabled_ir, scounteren_disabled_ir, mcounteren_enabled_ir, priv_mode_vu;
        cp_virtual_instr_vu_rv32_hedelegh: cross csrr, hedelegh, priv_mode_vu;
    `endif


    // Privilege mode crosses
    cp_loadstore_priv: cross
        hlvw_hlvxw_hsvw_instr,
        priv_mode_m_hs_vs_u_vu,
        hstatus_hu;


    cp_hfence_priv: cross
        sfencevma_hfencevvma_hfencegvma_instr,
        priv_mode_m_hs_vs_u_vu,
        mstatus_tvm,
        hstatus_vtvm;


    // HLV address misalignment crosses
    cp_hlv_address_misaligned: cross hlv_instructions, hlv_adr_LSBs, priv_mode_m;


    // access fault coverpoints
    `ifdef RVMODEL_ACCESS_FAULT_ADDRESS
        illegal_address: coverpoint ins.current.imm + ins.current.rs1_val {
            bins illegal = {`RVMODEL_ACCESS_FAULT_ADDRESS};
        }
        // Illegal address for HLV/HSV instructions (rs1 only, no immediate)
        hlv_illegal_address: coverpoint ins.current.rs1_val {
            bins illegal = {`RVMODEL_ACCESS_FAULT_ADDRESS};
        }
        address_legality: coverpoint ((ins.current.imm + ins.current.rs1_val) & ~(64'h3)) == (`RVMODEL_ACCESS_FAULT_ADDRESS & ~(64'h3)) {
            bins legal = {0};
            bins illegal = {1};
        }

        // HLV access fault crosses
        cp_hlv_access_fault: cross hlv_instructions, hlv_illegal_address, priv_mode_m;
        // HSV access fault crosses
        cp_hsv_access_fault: cross hsv_instructions, illegal_address, priv_mode_m;
        // xtinst access faults
        cp_xtinst_instr_access: cross jalr, illegal_address, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
        cp_xtinst_load_access: cross loadop, illegal_address, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
        cp_xtinst_store_access: cross storeop, illegal_address, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
        // hedeleg access faults
        cp_hedeleg_instr_access_fault: cross jalr, illegal_address, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
        cp_hedeleg_load_access_fault: cross loadop, illegal_address, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
        cp_hedeleg_store_access_fault: cross storeop, illegal_address, priv_mode_m_hs_vs_u_vu, medeleg_delegation, hedeleg_delegation;
        // Priority crosses
        // checking for legal addresses is not as rigorous right now
        cp_priority: cross
            hlv_hsv_instr,
            address_legality,
            addr_alignment,
            priv_mode_m_hs_vs_u_vu,
            hstatus_hu;

    `endif

    // HSV address misalignment crosses
    cp_hsv_address_misaligned: cross hsv_instructions, adr_LSBs, priv_mode_m;

    // HTINST/XTINST crosses - transformed instruction encoding
    cp_xtinst_instr_misaligned: cross jal, pc_bit_1, imm_bit_1, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
    cp_xtinst_illegalinstr: cross illegalop, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
    cp_xtinst_breakpoint: cross ebreak, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
    cp_xtinst_virtinstr: cross csrr, vstval, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
    cp_xtinst_load_misaligned: cross loadop, addr_misaligned, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
    cp_xtinst_store_misaligned: cross storeop, addr_misaligned, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
    cp_xtinst_ecall: cross ecall, priv_mode_vs, medeleg_except_ecall, hedeleg_disabled;
endgroup


function void exceptionsh_sample(int hart, int issue, ins_t ins);
    ExceptionsH_cg.sample(ins);
endfunction
