///////////////////////////////////////////
//
// RISC-V Architectural Functional Coverage Covergroups
//
// Written: Corey Hickson chickson@hmc.edu 29 November 2024
//
// Copyright (C) 2024 Harvey Mudd College, 10x Engineers, UET Lahore, Habib University
//
// SPDX-License-Identifier: Apache-2.0
//
////////////////////////////////////////////////////////////////////////////////////////////////

`define COVER_EXCEPTIONSZALRSC
covergroup ExceptionsZalrsc_cg with function sample(ins_t ins);
    option.per_instance = 0;

    // building blocks for the main coverpoints
    lr: coverpoint ins.current.insn {
        wildcard bins lr_w = {LR_W};
        `ifdef UDB_MXLEN_64
            wildcard bins lr_d = {LR_D};
        `endif
    }
    sc: coverpoint ins.current.insn {
        wildcard bins sc_w = {SC_W};
        `ifdef UDB_MXLEN_64
            wildcard bins sc_d = {SC_D};
        `endif
    }
    lr_w: coverpoint ins.current.insn {
    wildcard bins lr_w = {LR_W};
    }
    sc_w: coverpoint ins.current.insn {
        wildcard bins sc_w = {SC_W};
    }
    `ifdef UDB_MXLEN_64
        lr_d: coverpoint ins.current.insn {
            wildcard bins lr_d = {LR_D};
        }
        sc_d: coverpoint ins.current.insn {
                wildcard bins sc_d = {SC_D};
        }
        adr_LSBs_illegal_d: coverpoint ins.current.rs1_val[2:0]  {
        ignore_bins zero = {0};
        }
        adr_LSBs_legal_d: coverpoint ins.current.rs1_val[2:0]  {
            bins zero = {0};
        }
    `endif

    adr_LSBs_illegal_w: coverpoint ins.current.rs1_val[2:0]  {
        ignore_bins zero = {0};
        ignore_bins four = {3'b100};
    }
    adr_LSBs_legal_w: coverpoint ins.current.rs1_val[2:0]  {
        bins zero = {0};
        bins four = {3'b100};
    }
    rd_gt_one_prev: coverpoint ins.prev.rd_val {
        bins nonzero = {[2:$]};
    }
    rd_gt_one_cur: coverpoint ins.current.rd_val {
        bins nonzero = {[2:$]};
    }
    rd_zero_cur: coverpoint ins.current.rd_val {
        bins zero = {0};
    }
    adr_LSBs: coverpoint ins.current.rs1_val[2:0]  {
        // auto fills 000 through 111
    }

    // Ideally this coverpoint would check the rs1 of the paired lr.x and sc.x
    // to match the test plan. With our current toolflow
    // Testing that the rs1 value of the previous lr.x matches the rs1 of the sc.x
    // is not possible.
    // As a proxy we can check that the rd value changes as expected for rd when an
    // sc.x instruction is executed. Load rd with a value > 1.
    // Faulting instructions should not modify the rd value while
    // Successful instructions should write 0 to rd.
    // sc.w and sc.d have a different set of legal LSB offset
    // (4 byte aligned vs ) so they are handled differently in the coverpoints.

    // main coverpoints


    // access fault coverpoints
    `ifdef RVMODEL_ACCESS_FAULT_ADDRESS
        illegal_address: coverpoint ins.current.rs1_val {
            bins illegal = {`RVMODEL_ACCESS_FAULT_ADDRESS};
        }
        illegal_address_misaligned: coverpoint ins.current.rs1_val {
            bins illegal = {`RVMODEL_ACCESS_FAULT_ADDRESS + 1};
        }
        non_illegal_address: coverpoint ({{ins.current.imm + ins.current.rs1_val}[`UDB_MXLEN-1:3], 3'b000} != `RVMODEL_ACCESS_FAULT_ADDRESS) {
            bins non_illegal = {1};
        }
        cp_load_address_misaligned:                cross lr, adr_LSBs, non_illegal_address;
        cp_load_access_fault:                      cross lr, illegal_address;
        cp_load_access_misaligned_priority:        cross lr, illegal_address_misaligned;
        cp_store_address_misaligned_legal_w:       cross sc_w, adr_LSBs_legal_w,rd_gt_one_prev, rd_zero_cur, non_illegal_address;
        cp_store_address_misaligned_illegal_w:     cross sc_w, adr_LSBs_illegal_w, rd_gt_one_prev, rd_gt_one_cur, non_illegal_address;
        // illegal sc.w and sc.d does not get coverage as SAIL stores content in by bytes instead of giving exceptions
        // Sail issue: https://github.com/riscv/sail-riscv/issues/1574
        `ifdef UDB_MXLEN_64
            cp_store_address_misaligned_legal_d:     cross sc_d, adr_LSBs_legal_d,rd_gt_one_prev, rd_zero_cur, non_illegal_address;
            cp_store_address_misaligned_illegal_d:   cross sc_d, adr_LSBs_illegal_d, rd_gt_one_prev, rd_gt_one_cur, non_illegal_address;
        `endif
        cp_store_access_fault:                cross sc, illegal_address, rd_gt_one_prev, rd_gt_one_cur;
        cp_store_access_misaligned_priority:  cross sc, illegal_address_misaligned, rd_gt_one_prev, rd_gt_one_cur;
    `else // if no access faults, exclude non_illegal_address condition
        cp_load_address_misaligned:                cross lr, adr_LSBs;
        cp_store_address_misaligned_legal_w:       cross sc_w, adr_LSBs_legal_w,rd_gt_one_prev, rd_zero_cur;
        cp_store_address_misaligned_illegal_w:     cross sc_w, adr_LSBs_illegal_w, rd_gt_one_prev, rd_gt_one_cur;
        `ifdef UDB_MXLEN_64
            cp_store_address_misaligned_legal_d:     cross sc_d, adr_LSBs_legal_d,rd_gt_one_prev, rd_zero_cur;
            cp_store_address_misaligned_illegal_d:   cross sc_d, adr_LSBs_illegal_d, rd_gt_one_prev, rd_gt_one_cur;
        `endif
    `endif
endgroup

function void exceptionszalrsc_sample(int hart, int issue, ins_t ins);
    ExceptionsZalrsc_cg.sample(ins);
    //  $display("adr_LSBs: %b, op[6:0]: %b, rd_boolean: %b",
    //   ins.current.rs1_val[2:0],
    //   ins.current.insn[6:0],
    //   ins.current.rd_val);
endfunction
