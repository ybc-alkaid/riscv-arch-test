///////////////////////////////////////////
//
// RISC-V Architectural Functional Coverage Covergroups
//
// Written: Corey Hickson chickson@hmc.edu 23 November 2024
//
// Copyright (C) 2024 Harvey Mudd College, 10x Engineers, UET Lahore, Habib University
//
// SPDX-License-Identifier: Apache-2.0
//
////////////////////////////////////////////////////////////////////////////////////////////////

`define COVER_EXCEPTIONSF
covergroup ExceptionsF_cg with function sample(ins_t ins);
    option.per_instance = 0;

    // building blocks for the main coverpoints
    mstatus_FS_zero: coverpoint ins.prev.csr[CSR_MSTATUS][14:13] {
        bins disabled = {2'b00};
    }
    mstatus_FS_status: coverpoint ins.prev.csr[CSR_MSTATUS][14:13] {
        bins fs_initial = {2'b01};
        bins fs_clean   = {2'b10};
        bins fs_dirty   = {2'b11};
    }
    frm_legal: coverpoint ins.prev.csr[CSR_FCSR][7:5] {
        bins legal_frm = {3'b000, 3'b001, 3'b010, 3'b011, 3'b100};
    }
    instrs: coverpoint ins.current.insn {
        wildcard bins fsw          = {FSW};
        wildcard bins flw          = {FLW};
        wildcard bins fadd         = {FADD_S};
        wildcard bins fsub         = {FSUB_S};
        wildcard bins fmul         = {FMUL_S};
        wildcard bins fdiv         = {FDIV_S};
        wildcard bins fcvt_x_f     = {FCVT_W_S};
        wildcard bins fcvt_f_x     = {FCVT_S_W};
        `ifdef D_SUPPORTED
            wildcard bins fcvt_f_f     = {FCVT_S_D};
        `endif
        wildcard bins fmadd        = {FMADD_S};
        wildcard bins fsqrt        = {FSQRT_S};
        wildcard bins fsgnj        = {FSGNJ_S};
        wildcard bins feq          = {FEQ_S};
        wildcard bins fmv_x_f      = {FMV_X_S};
        wildcard bins fmv_f_x      = {FMV_S_X};
        wildcard bins fclass       = {FCLASS_S};
        wildcard bins fmin         = {FMIN_S};
        `ifdef ZFA_SUPPORTED
            wildcard bins fli          = {FLI_S};
            wildcard bins fround       = {FROUND_S};
            `ifdef UDB_MXLEN_32
                `ifdef D_SUPPORTED
                    wildcard bins fmvh         = {FMVH_X_D};
                    wildcard bins fmvp         = {FMVP_D_X};
                `endif
            `endif
        `endif
    }
    csr_instrs: coverpoint ins.current.insn {
        wildcard bins csrrw_fcsr   = {32'b000000000011_?????_001_?????_1110011};
        wildcard bins csrrw_frm    = {32'b000000000010_?????_001_?????_1110011};
        wildcard bins csrrw_fflags = {32'b000000000001_?????_001_?????_1110011};
        wildcard bins csrrs_fcsr   = {32'b000000000011_?????_010_?????_1110011};
        wildcard bins csrrs_frm    = {32'b000000000010_?????_010_?????_1110011};
        wildcard bins csrrs_fflags = {32'b000000000001_?????_010_?????_1110011};
        wildcard bins csrrc_fcsr   = {32'b000000000011_?????_011_?????_1110011};
        wildcard bins csrrc_frm    = {32'b000000000010_?????_011_?????_1110011};
        wildcard bins csrrc_fflags = {32'b000000000001_?????_011_?????_1110011};
    }
    csr_writes: coverpoint ins.current.insn {
        wildcard bins csrrw_fcsr   = {32'b000000000011_?????_001_?????_1110011};
        wildcard bins csrrw_frm    = {32'b000000000010_?????_001_?????_1110011};
        wildcard bins csrrw_fflags = {32'b000000000001_?????_001_?????_1110011};
        wildcard bins csrrs_fcsr   = {32'b000000000011_?????_010_?????_1110011};
        wildcard bins csrrs_frm    = {32'b000000000010_?????_010_?????_1110011};
        wildcard bins csrrs_fflags = {32'b000000000001_?????_010_?????_1110011};
        wildcard bins csrrc_fcsr   = {32'b000000000011_?????_011_?????_1110011};
        wildcard bins csrrc_frm    = {32'b000000000010_?????_011_?????_1110011};
        wildcard bins csrrc_fflags = {32'b000000000001_?????_011_?????_1110011};
    }
    loadops: coverpoint ins.current.insn {
        wildcard bins flw = {FLW};
        `ifdef ZFHMIN_SUPPORTED
            wildcard bins flh = {FLH};
        `endif
        `ifdef D_SUPPORTED
            wildcard bins fld = {FLD};
        `endif
        `ifdef Q_SUPPORTED
            wildcard bins flq = {FLQ};
        `endif
    }
    storeops: coverpoint ins.current.insn {
        wildcard bins fsw = {FSW};
        `ifdef ZFHMIN_SUPPORTED
            wildcard bins fsh = {FSH};
        `endif
        `ifdef D_SUPPORTED
            wildcard bins fsd = {FSD};
        `endif
        `ifdef Q_SUPPORTED
            wildcard bins fsq = {FSQ};
        `endif
    }
    adr_LSBs: coverpoint {ins.current.rs1_val + ins.current.imm}[3:0]  {
        // auto fills 0000 through 1111
    }


    // main coverpoints
    cp_mstatus_fs_illegal_instr: cross instrs, mstatus_FS_zero;
    cp_mstatus_fs_csr_write:     cross csr_writes, mstatus_FS_zero;
    cp_mstatus_fs_legal:         cross instrs, mstatus_FS_status, frm_legal;
    cp_load_address_misaligned:  cross loadops, adr_LSBs;
    cp_store_address_misaligned: cross storeops, adr_LSBs;

    // access fault coverpoints
    `ifdef RVMODEL_ACCESS_FAULT_ADDRESS
        illegal_address: coverpoint ins.current.imm + ins.current.rs1_val {
            bins illegal = {`RVMODEL_ACCESS_FAULT_ADDRESS};
        }
        cp_load_access_fault:        cross loadops, illegal_address;
        cp_store_access_fault:       cross storeops, illegal_address;
    `endif
endgroup

function void exceptionsf_sample(int hart, int issue, ins_t ins);
    //$display("Mstatus FS: %b, frmIllegal: %b, op: %b, fmrBits: %b, imm: %b", ins.current.csr[CSR_MSTATUS][14:13], ins.current.csr[CSR_FCSR][7:5],  ins.current.insn[6:0], ins.current.insn[14:12], ins.current.insn[31:27]);
    ExceptionsF_cg.sample(ins);


endfunction
