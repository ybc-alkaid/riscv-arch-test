///////////////////////////////////////////
//
// RISC-V Architectural Functional Coverage Covergroups
//
// Written: Corey Hickson chickson@hmc.edu 16 Feb 2025, Sadhvi Narayanan sanarayanan@hmc.edu April 2026
//
// Copyright (C) 2024 Harvey Mudd College, 10x Engineers, UET Lahore, Habib University
//
// SPDX-License-Identifier: Apache-2.0
//
////////////////////////////////////////////////////////////////////////////////////////////////

`define COVER_INTERRUPTSU
covergroup InterruptsU_cg with function sample(ins_t ins);
    option.per_instance = 0;
    `include "general/RISCV_coverage_standard_coverpoints.svh"

    // building blocks for the main coverpoints

    // Uses ins.prev instead of ins.current because RVVI updates CSRs after instruction retirement,
    // so ins.current shows post-trap state while ins.prev shows pre-trap state.
    mstatus_mie: coverpoint ins.prev.csr[CSR_MSTATUS][3]  {
        // autofill 0/1
    }
    mstatus_tw:  coverpoint ins.current.csr[CSR_MSTATUS][21] {
        // autofill 0/1
    }
    mstatus_tw_one: coverpoint ins.current.csr[CSR_MSTATUS][21] {
        bins one = {1};  // Only TW=1
    }
    mstatus_tw_zero: coverpoint ins.current.csr[CSR_MSTATUS][21] {
        bins zero = {0};  // Only TW=0
    }
    mie_msie_one: coverpoint ins.current.csr[CSR_MIE][3] {
        bins one = {1};
    }
    mie_mtie: coverpoint ins.current.csr[CSR_MIE][7] {
        // autofill 0/1
    }
    mie_mtie_one: coverpoint ins.current.csr[CSR_MIE][7] {
        bins one = {1};
    }
    mie_meie_one: coverpoint ins.current.csr[CSR_MIE][11] {
        bins one = {1};
    }
    mtvec_mode: coverpoint ins.current.csr[CSR_MTVEC][1:0] {
        bins direct   = {2'b00};
        bins vector   = {2'b01};
    }
    mip_msip:    coverpoint ins.current.csr[CSR_MIP][3]  {
        // autofill 0/1
    }
    mip_mtip:    coverpoint ins.current.csr[CSR_MIP][7]  {
        // autofill 0/1
    }
    mip_meip:    coverpoint ins.current.csr[CSR_MIP][11] {
        // autofill 0/1
    }
    wfi: coverpoint ins.current.insn {
        bins wfi = {WFI};
    }


    cp_user_mti:    cross priv_mode_u, mtvec_mode, mstatus_mie, mie_mtie_one, mip_mtip;
    cp_user_msi:    cross priv_mode_u, mtvec_mode, mstatus_mie, mie_msie_one,  mip_msip;

    // Note: External interrupts (MEI) not supported in Sail simulator yet
    cp_user_mei:    cross priv_mode_u, mtvec_mode, mstatus_mie, mie_meie_one,   mip_meip;
    cp_wfi:         cross priv_mode_u, wfi,        mstatus_mie, mstatus_tw_zero, mie_mtie_one;
    cp_wfi_timeout: cross priv_mode_u, wfi,        mstatus_mie, mstatus_tw_one, mie_mtie;

endgroup

function void interruptsu_sample(int hart, int issue, ins_t ins);
    InterruptsU_cg.sample(ins);

    // $display("=== InterruptsU Debug ===");
    // $display("PC: %h Instr: %s", ins.current.pc_rdata, ins.current.disass);
    // $display("  mstatus.MIE=%b mstatus.TW=%b mode: %b, vector: %b",
    //             ins.prev.csr[CSR_MSTATUS][3], ins.current.csr[CSR_MSTATUS][21], {ins.prev.mode_virt, ins.prev.mode}, {ins.current.csr[CSR_MTVEC][1:0]});
    // $display("  mie: MEIE=%b MTIE=%b MSIE=%b (full=%h)",
    //             ins.current.csr[CSR_MIE][11], ins.current.csr[CSR_MIE][7],
    //             ins.current.csr[CSR_MIE][3], ins.current.csr[CSR_MIE][15:0]);
    // $display("  mip: MEIP=%b MTIP=%b MSIP=%b (full=%h)",
    //             ins.current.csr[CSR_MIP][11], ins.current.csr[CSR_MIP][7],
    //             ins.current.csr[CSR_MIP][3], ins.current.csr[CSR_MIP]);
    // if (ins.current.trap)
    //     $display("  TRAP! mcause=%h", ins.current.csr[CSR_MCAUSE]);
    // $display("");
endfunction
